plugins {
    id "java"
    id "com.github.kamatama41.embulk" version "0.5.0"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven { url 'http://kamatama41.github.com/maven-repository/repository' }
}

configurations {
    [compileClasspath, testCompileClasspath].each {
        it.resolutionStrategy.activateDependencyLocking()
    }
}

dependencies {
    compile("com.github.kamatama41:nsocket:0.2.10") {
        // Use embulk-core's Jackson (2.6.7) instead
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
    }
    // Followings are needed for running EmbulkServer
    implementation "org.embulk:embulk-standards:0.9.16"
    implementation "ch.qos.logback:logback-classic:1.2.3"
    implementation "ch.qos.logback:logback-core:1.2.3"
}

task executableEmbulkServer(type: Jar) {
    manifest {
        attributes 'Main-Class': 'org.embulk.executor.remoteserver.Launcher'
    }
    baseName = 'embulk-server'
    from { (configurations.runtime).collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

embulk {
    version = "0.9.16"
    category = "executor"
    name = "docker"
    authors = ["Shinichi Ishimura"]
    email = "shiketaudonko41@gmail.com"
    description = "Embulk executor plugin to run plugins on Docker container."
    licenses = ["MIT"]
    homepage = "https://github.com/kamatama41/embulk-executor-docker"
}
